macro -;
macro [100000;
macro *10 100000;
macro=;

warn -;
hold 10000;
mult: hold 30 replic 50;
ckeep;
log log; log/;

/*User specifies 5 chars in args.*/

var: charray[5] mxst trnum baselength a b c d e max s corr];

set charray [0] %1;
set charray [1] %2;
set charray [2] %3;
set charray [3] %4;
set charray [4] %5;

set a nstates['charray[0]'];
set b nstates['charray[1]'];
set c nstates['charray[2]'];
set d nstates['charray[3]'];
set e nstates['charray[4]'];

loop=blanks 0 nchar
xread= #blanks 1 -;
stop

set mxst maxstate['charray[0]'];
loop 1 4							/*Do any of the other 4 chars have more states?*/
if (maxstate['charray[#1]'] > 'mxst')
	set mxst maxstate['charray[#1]'];
	end
stop

var: steparray [('mxst'+1), ('mxst'+1), ('mxst'+1), ('mxst'+1), ('mxst'+1)];

collapse 3; collapse [;
taxcode -1;
if (ntax > 14)
	mult; else
	ie; end
best;
taxcode +1;
set baselength length[0];
	quote BASELENGTH 'baselength';
set trnum ntrees;
tsave * tntrees.tre;
save; tsave/;
cc !;

loop =stch0 0 ('a'-1)
xread= 'charray[0]' 1 #stch0;

loop =stch1 0 ('b'-1)
xread= 'charray[1]' 1 #stch1;

loop =stch2 0 ('c'-1)
xread= 'charray[2]' 1 #stch2;

loop =stch3 0 ('d'-1)
xread= 'charray[3]' 1 #stch3;

loop =stch4 0 ('e'-1)
xread= 'charray[4]' 1 #stch4;

loop =constree 0 'trnum'
	keep 0;
	proc tntrees.tre;
	force >1;
	force / & #constree;
	constrain=;
silent=all;

if (ntax > 14)
	mult; else
	ie; end
best;
silent-all;
if (#constree == 0)
	set steparray [#stch0,#stch1,#stch2,#stch3,#stch4] (length[0] - 'baselength');
	end
if ((#constree != 0) && ((length[0] - 'baselength') < 'steparray [#stch0,#stch1,#stch2,#stch3,#stch4]'))
	set steparray [#stch0,#stch1,#stch2,#stch3,#stch4] (length[0] - 'baselength');
	log+log; quote TREE #constree IS SHORTER; log/;
	end
if ((#constree !=0) && ((length[0] - 'baselength') >= 'steparray [#stch0,#stch1,#stch2,#stch3,#stch4]'))
	continue;
	end

force ]; constrain -;
stop /*constree*/
stop /*stch4*/
stop /*stch3*/
stop /*stch2*/
stop /*stch1*/
stop /*stch0*/

set max ((4*'a'*'b'*'c'*'d'*'e')-('a'*'b'*'c'*'d')-('a'*'b'*'c'*'e')-('a'*'b'*'d'*'e')-('a'*'c'*'d'*'e')-('b'*'c'*'d'*'e')+1);
set s 0; 
loop =x1 0 ('a'-1)
loop =x2 0 ('b'-1) 
loop =x3 0 ('c'-1)
loop =x4 0 ('d'-1)
loop =x5 0 ('e'-1)
set s+= 'steparray[#x1,#x2,#x3,#x4,#x5]';
log +log; quote CELL #x1 x #x2 x #x3 x #x4 x #x5 IS 'steparray[#x1,#x2,#x3,#x4,#x5]'; log/;
stop
stop
stop
stop
stop
set corr ('s'/'max');
log +log; quote Characters 'charray[0]', 'charray[1]', 'charray[2]', 'charray[3]' and 'charray[4]' have s = 's', max = 'max', and r = 'corr'; log/;

loop=p 0 ('mxst'-1);
loop=q 0 ('mxst'-1);
loop=r 0 ('mxst'-1);
loop=s 0 ('mxst'-1);
loop=t 0 ('mxst'-1);
set steparray[#p,#q,#r,#s,#t] 0;
stop
stop
stop
stop
stop

loop=blanks 0 nchar
xread= #blanks 1 -;
stop

force ];
keep 0;
proc/;
